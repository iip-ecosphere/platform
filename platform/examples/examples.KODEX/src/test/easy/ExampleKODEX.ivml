project ExampleKODEX {

    import IIPEcosphere;
    import DataTypes;

    annotate BindingTime bindingTime = BindingTime::compile to .;

    // ------------ component setup ------------------

    serializer = Serializer::Json;
    // serviceManager, containerManager are already defined    
        
    aasServer = {
        schema = AasSchema::HTTP,
        port = 9001,
        host = "127.0.0.1"
    };
    aasRegistryServer = {
        schema = AasSchema::HTTP,
        port = 9002,
        host = "127.0.0.1",
        path = "registry"
    };
    aasImplServer = {
        port = 9003
    };
    aasProtocol = AasProtocolVabTcp{};
    
    // ------------------ transport --------------------------
    
    transportProtocol = TransportProtocolAMQP {
        port = 8883,
        user = "user",
        password = "pwd"
    };
    
    serviceProtocol = ServiceProtocolAMQP {};
        
    // ------------------ resources/devicemgt ----------------
    
    deviceRegistry = BasicDeviceRegistry {
    };
    
    deviceMgtStorage = S3MockDeviceMgtStorage {
        host = "localhost",
        port = 8884,
        region = "local"
    };
    
    // just for testing, this instantiates app rather than platform
    deviceMgtStorageServer = S3MockDeviceMgtStorageServer{};

    // ------------ data types ------------------
    
    RecordType rec1 = {
        name = "KRec13",
        fields = {
            Field {
                name = "intField",
                type = refBy(IntegerType)
            }, Field {
                name = "stringField",
                type = refBy(StringType)
            }
        }
    };
    
    RecordType rec2 = {
        name = "KRec13Anon",
        fields = {
            Field {
                name = "_kip",
                type = refBy(StringType)
            }, Field {
                name = "intField",
                type = refBy(IntegerType)
            }, Field {
                name = "stringField",
                type = refBy(StringType)
            }
        }
    };
    
    // ------------ individual, reusable services ------------------
        
    Service mySourceService = JavaService {
        id = "SimpleKodexSource",
        name = "Simple Kodex Data Source",
        description = "",
        ver = "0.1.0",
        deployable = true,
        asynchronous = true,
        class = "de.iip_ecosphere.platform.examples.kodex.SimpleSourceKodexImpl",
        artifact = "de.iip-ecosphere.platform:examples.KODEX:" + iipVer,
        kind = ServiceKind::SOURCE_SERVICE,
        output = {{type=refBy(rec1)}},
        traceSent = TraceKind::LOG
    };

    Service myKodexService = KodexService {
        id = "Pseudonymizer",
        name = "Kodex Pseudonymizer",
        description = "",
        ver = "0.1.0",
        deployable = true,
        input = {{type=refBy(rec1)}},
        output = {{type=refBy(rec2)}},
        actions = {{field=refBy(rec1.fields[1]), action=KodexAction::PseudonymizeMerengue}}, 
        traceRcv = TraceKind::LOG,
        traceSent = TraceKind::LOG
    };
    
    Service myReceiverService = JavaService {
        id = "KodexReceiver",
        name = "Kodex Data Receiver",
        description = "",
        ver = "0.1.0",
        deployable = true,
        class = "de.iip_ecosphere.platform.examples.kodex.KodexReceiverImpl",
        artifact = "de.iip-ecosphere.platform:examples.KODEX:" + iipVer,
        kind = ServiceKind::SINK_SERVICE,
        input = {{type=refBy(rec2)}},
        traceRcv = TraceKind::LOG
    };

    // ------------ application and service nets ------------------
    
    Application myApp = {
        id = "KodexMeshApp",
        name = "Simple Kodex Testing App",
        ver = "0.1.0",
        description = "",
        services = {refBy(myMesh)}        
    };
    
    ServiceMesh myMesh = {
        description = "initial service net",
        sources = {refBy(mySource)}
    };
    
    MeshSource mySource = {
        pollInterval = 800, // only if source is synchronous
        impl = refBy(mySourceService),
        next = {refBy(myConnMySourceMyTransformer)}
    };
    
    MeshConnector myConnMySourceMyTransformer = {
        name = "Source->Kodex",
        next = refBy(myTransformer)
    };

    MeshProcessor myTransformer = {
        impl = refBy(myKodexService),
        next = {refBy(myConnMyPythonMyReceiver)}
    };

    MeshConnector myConnMyPythonMyReceiver = {
        name = "Python->Receiver",
        next = refBy(myReceiver)
    };

    MeshSink myReceiver = {
        impl = refBy(myReceiverService)
    };
    
    // ---------- generation setup ------------
    
    sharedInterfaces = true;
    sharedArtifact = "de.iip-ecosphere.platform:apps.AppInterfacesKodexExample:" + iipVer; // for testing all-in-one

    // ------------ freezing ------------------
    
    freeze {
        aas;
        aasServer;
        aasRegistryServer;
        aasImplServer;
        aasPersistency;
        aasProtocol;
        serializer;
        transportProtocol;
        serviceManager;
        serviceProtocol;
        containerManager;
        deviceMgtStorageServer;
        deviceMgtStorage;
        deviceRegistry;
        javaModuleOpts;
        javaOpts;
        pidDir;
        sharedInterfaces;
        sharedArtifact;
        .; // every variable declared in this project
    } but (f|f.bindingTime >= BindingTime.runtimeMon);

}