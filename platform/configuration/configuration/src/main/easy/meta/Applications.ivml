project Applications {

    import Connectors;
    import DataTypes;
    import Nameplate;
    import ServerPart*;
    
    annotate BindingTime bindingTime = BindingTime::compile to .;

    compound Application refines VersionedElement {
        Id id;
        NonEmptyNameString name;
        String description = "";
        String artifact = "";
        setOf(refTo(ServiceMesh)) services; // no services is ok, then the artifact must have hand-crafted services
        NameplateInfo nameplateInfo;
        Boolean createContainer = false;
        Boolean debug = false;
        String cmdArg; // for now, comma separated
        setOf(refTo(Server)) servers = {}; // additional server instances to be started 
        
        Constraint idOk = matches(id, "^[^@]+$");
    }
    
    // deprecated, use mvnVersion from DataTypes instead
    def String appVersion(Application a) = 
       mvnVersion(a);
    
    Constraint appNamesUnique = allInstances(Application)->isUnique(a|a.name);
    Constraint appIdUnique = allInstances(Application)->isUnique(a|a.id);
    
    compound ServiceMesh {
        String description;
        setOf(refTo(MeshSource)) sources;

        Constraint meshElementsUnique = self.sources->closure(refTo(MeshElement) e|nextElts(e))
            .toSequence()->isUnique(e|e.name);
    }
    
    compound MeshElement {
        String name; // no specific string, pulled up
        ServiceKind kind;
        refTo(ServiceBase) impl; // allow for family or service
        refTo(MeshElement) ensemble = null; // in same process as referenced, usually not; here not in service to facilitate service reuse
        assign (bindingTime = BindingTime::runtimeEnact) to {
            Integer pos_x = -1;
            Integer pos_y = -1;
        }
        
        Constraint implName = name == impl.name; // propagate
        Constraint implKind = kind == impl.kind; // propagate
        Constraint ensembleOk = ensemble <> null implies (ensemble <> refBy(self) and not(ensemble.ensemble <> null)); // no self-loops, max 1-chains
    }

    def setOf(refTo(MeshElement)) nextElts(MeshElement elt) = {};
    def setOf(refTo(MeshElement)) nextElts(MeshSource src) = src.next->collect(c|c.next);
    def setOf(refTo(MeshElement)) nextElts(MeshInnerElement elt) = elt.next->collect(c|c.next);
    
    // hint to requirements!
    compound MeshConnector refines CConnector {
        refTo(MeshElement) next;
    }
    
    compound MeshSource refines MeshElement {
        setOf(refTo(MeshConnector)) next;
        Integer pollInterval = 1000; //in ms, ignored if negative or asynchronous
        sequenceOf(IOType) output;

        Constraint sourceOut = output == impl.output;
        Constraint sourceKind = kind == ServiceKind::SOURCE_SERVICE;
        Constraint inOutCheck = next->forAll(n|typeCheck(self, n.next));
    }
    
    abstract compound MeshInnerElement refines MeshElement {
        setOf(refTo(MeshConnector)) next;
    }
    
    compound MeshProcessor refines MeshInnerElement {
        sequenceOf(IOType) input;
        sequenceOf(IOType) output;

        Constraint procIn = input == impl.input;
        Constraint procOut = output == impl.output;
        Constraint procKind = kind == ServiceKind::TRANSFORMATION_SERVICE or kind == ServiceKind::PROBE_SERVICE;
        Constraint inOutCheck = next->forAll(n|typeCheck(self, n.next));
    }
    
    compound MeshSink refines MeshInnerElement {
        sequenceOf(IOType) input;
        
        Constraint sinkIn = input == impl.input;
        Constraint sinkKind = kind == ServiceKind::SINK_SERVICE;
    }
    
    // output-input check, selection via dynamic dispatch, all relevant combinations needed due to type-specific setup 
    // of i/o above; if not both sides are defined, propagate them along the service mesh; if propagation fails, mesh
    // is anyway invalid; currently only check for unique type names 
    
    def Boolean typeCheck(MeshElement start, MeshElement end) = 
        true;
    
    def Boolean typeCheck(MeshSource start, MeshSink end) =
        typeCheck(start.output, end.input); 
         
    def Boolean typeCheck(MeshSource start, MeshProcessor end) =
        typeCheck(start.output, end.input); 
        
    def Boolean typeCheck(MeshProcessor start, MeshSink end) =
        typeCheck(start.output, end.input); 

    def Boolean typeCheck(sequenceOf(IOType) start, sequenceOf(IOType) end) = 
        fwdTypes(start)->collect(IOType t|t.type.name).overlaps(fwdTypes(end)->collect(IOType t|t.type.name));
    
   // ------------------------------ platform trace -> AAS service -------------------

   // data ignored for now, only tracing   
   compound TraceToAasService refines CompleteService {
       ServiceKind kind = ServiceKind::SINK_SERVICE;
       IipVersion ver = "0.1.0";
       String artifact = ""; // ships with platform in service environment
       refTo(Application) application;
       NameplateInfo nameplateInfo = {
           manufacturerProductDesignation = "Application AAS with Trace embedding@en",
           manufacturer = refBy(manufacturer_sse)
       };
       Parameters parameter = {
         LongParameter{name="timeout", defaultValue=3600000}
       };
   }
   
   // ------------------------------- deployment --------------------------------------
   
   // deployment plan, outside application allowing to keep the plan local while application can be shared
   // explicit container specificiation is currently not intended, the artifact is derived
   compound DeploymentPlan {
       NonEmptyString name;
       refTo(Application) application;
       NonEmptyString id;
       IipVersion ver = "0.0.0";
       String description = "";
       sequenceOf(ServiceAssignment) assignments;
       sequenceOf(EnsembleSpec) ensembles;
       Boolean parallelize = false;
       Boolean onUndeployRemoveArtifact = false;
       Boolean disabled = false;
       String cmdArg; // for now, comma separated as in application
       
       Constraint hasAssignments = isDefined(self.assignments) and self.assignments.size() > 0;
       Constraint meshCovered = sources(self.application)->closure(refTo(MeshElement) e|nextElts(e)) 
           == self.assignments->collect(a|a.services).flatten().toSet();
       Constraint noLoopEnsembles = not(self.ensembles->collect(e|e.member).overlaps(self.ensembles->collect(e|e.leader)));
   }
   
   def setOf(refTo(MeshElement)) sources(Application app) = app.services->collect(m|m.sources).flatten().selectByKind(refTo(MeshElement));

   Constraint deployPlanIdUnique = allInstances(DeploymentPlan)->isUnique(d|d.id);
   
   compound EnsembleSpec {
       refTo(MeshElement) leader;
       refTo(MeshElement) member;
   }
   
   abstract compound ServiceAssignment {
       sequenceOf(refTo(MeshElement)) services;
   }
   
   // basic version as for now, future may reference to Resource
   compound NamedServiceAssignment refines ServiceAssignment {
       NonEmptyString resource;
   }
   
   // ids unique
   // service assignments defined, not duplicate and complete
   
}