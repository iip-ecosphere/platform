import JavaBasics;
import JavaMapping;
import Basics;
import MeshBasics;
import DockerBasics;
import IIPEcosphereBase;

@advice(IIPEcosphere)
vilScript IIPEcospherePartDocker (Project source, Configuration config, Project target) extends IIPEcosphereBase {

    String createContainerBuildScript(DockerContainerManager mgr, Application a, sequenceOf(MeshElement) elements, 
        Path appRoot, ContainerType contType, Integer isPythonService, EcsDevice device) = {
        String vtlFile;
        String targetFile;
        Path df = "${appRoot}/../app.Dockerfile.txt";
        
        if (contType == ContainerType::Ecs) {
            vtlFile = "Dockerfile_3_ecs";
            targetFile = "ecs.Dockerfile";   
            df = "${appRoot}/../app.Dockerfile.Ecs";
        }
		
        if (contType == ContainerType::Ecs_Svc_App) {
            if (isPythonService == 0) {
                // JAVA         
                vtlFile = "Dockerfile";
                targetFile = "Dockerfile";   
            } else {
                // JAVA & PYTHON
                vtlFile = "Dockerfile_Python";
                targetFile = "Dockerfile";   
            }
        }
                
        if (contType == ContainerType::EcsSvc_App) {
             if (isPythonService == 0) {
                // JAVA         
                vtlFile = "Dockerfile";
                targetFile = "Dockerfile";   
             } else {
                // JAVA & PYTHON
                vtlFile = "Dockerfile";
                targetFile = "Dockerfile_Python";   
            }
        }   

        if (contType == ContainerType::C1Ecs_C2Svc_App) {
            if (isPythonService == -1) { // Container with ECS
                vtlFile = "Dockerfile_3_ecs";
                targetFile = "ecs.Dockerfile";   
                df = "${appRoot}/../app.Dockerfile.Ecs";
            } else if (isPythonService == 0) { // Container with ServiceMgr and App
                // JAVA         
                vtlFile = "Dockerfile";
                targetFile = "app.Dockerfile";   
            } else { // Container with ServiceMgr and App
                // JAVA & PYTHON
                vtlFile = "Dockerfile_Python";
                targetFile = "app.Dockerfile";   
            }
        }
                
        vilTemplateProcessor(vtlFile, config, df, elements=elements, app=a, containerType=contType, device=device);
        df.rename(targetFile);
        targetFile;
    }
    
    String createContainerImage(DockerContainerManager mgr, Path base, Path buildFile, String registry, String repository, String tag) = {
        String id = dockerBuildImage(base, buildFile, toDockerImageName(registry, repository, tag));
        if (isDefined(mgr.registry) and mgr.registry.length() > 0) {
            dockerPushImage(toDockerImageName(registry, repository, tag), mgr.registry, registry.toLower() + "/" + repository, tag);
        }
        id;
    }
    
    loginContainerRegistry(DockerContainerManager mgr, String user, String password) = {
        if (isDefined(mgr.registry) and mgr.registry <> "") {
            dockerLogin(mgr.registry, user, password);
        }
    }

    logoutContainerRegistry(DockerContainerManager mgr) = {
        if (isDefined(mgr.registry) and mgr.registry <> "") {
            dockerLogout(mgr.registry);
        }
    }

}
