project SerializerConfig1Old {

    import IIPEcosphere;
    import DataTypes;

    annotate BindingTime bindingTime = BindingTime::compile to .;

    // ------------ component setup ------------------

    serializer = Serializer::Json;
    // serviceManager, containerManager are already defined    
        
    aasServer = {
        schema = AasSchema::HTTP,
        port = 9001,
        host = "127.0.0.1"
    };
    aasRegistryServer = {
        schema = AasSchema::HTTP,
        port = 9002,
        host = "127.0.0.1",
        path = "registry"
    };
    aasImplServer = {
        port = 9003
    };
    aasProtocol = AasProtocolVabTcp{};

    // ------------------ transport --------------------------
    
    transportProtocol = TransportProtocolAMQP {
        port = 8883,
        user = "user",
        password = "pwd"
    };
    
    // ------------------ resources/devicemgt ----------------
    
    deviceRegistry = BasicDeviceRegistry {
    };
    
    deviceMgtStorage = S3MockDeviceMgtStorage {
        host = "localhost",
        port = 9005,
        region = "local"
    };
    
    // go for installed S3 server or cloud
    deviceMgtStorageServer = S3MockDeviceMgtStorageServer {
        port = 9005
    };

    // ------------ data types ------------------
    
    RecordType rec1 = {
        name = "Rec1",
        fields = {
            Field {
                name = "intField",
                type = refBy(IntegerType)
            }, Field {
                name = "stringField",
                type = refBy(StringType)
            }
        }
    };
    
    // ------------ individual, reusable services ------------------
        
    Service mySourceService = JavaService {
        id = "mySource",
        name = "mySource example",
        description = "",
        ver = "0.1.0",
        deployable = true,
        class = "MySourceImpl",
        artifact = "",
        kind = ServiceKind::SOURCE_SERVICE,
        output = {{type=refBy(StringType)}}
        // TODO parameter
    };
    
    RecordType myConnMachineIn = {
        name = "myConnMachineIn",
        fields = {
            Field {
                name = "dataField",
                type = refBy(StringType)
            }, Field {
                name = "axis",
                type = refBy(IntegerType)
            }
        }        
    };

    RecordType myConnMachineOut = {
        name = "myConnMachineOut",
        fields = {
            Field {
                name = "cmdField",
                type = refBy(StringType)
            }
        }        
    };
    
    RecordType myConnPltfIn = {
        name = "myConnPltfIn",
        fields = {
            Field {
                name = "axis",
                type = refBy(IntegerType)
            }, Field {
                name = "dataField",
                type = refBy(StringType)
            }
        }        
    };
    
    OpcUaV1Connector myOpcUaConn = {
        id = "myOpcConn",
        name = "myOpcConn example",
        description = "",
        ver = "0.1.0",
        port = 1234, // default localhost
        
        input = {{type=refBy(StringType)}}, // PRELIMINARY
        output = {{type=refBy(StringType)}}, // PRELIMINARY
        inInterface = refBy(myConnMachineIn), // PRELIMINARY
        outInterface = refBy(myConnMachineOut)  // PRELIMINARY
    };
    
    MqttV3Connector myMqttConn = {
        id = "myMqttConn",
        name = "myMqttConnExample",
        description = "",
        ver = "0.1.0",
        port = 4321, // default localhost
        
        input = {{type=refBy(StringType)}}, // PRELIMINARY
        output = {{type=refBy(myConnPltfIn)}},
        inChannel = "IN",
        outChannel = "OUT",
        inInterface = refBy(myConnMachineIn),
        outInterface = refBy(myConnMachineOut),
        machineFormatter = TextLineFormatter{separator=",", mapping={}, transferType="String"},
        machineParser = TextLineParser{separator=",", mapping={}, transferType="String"},
        operations = {
          AssignmentOperation{field=myConnPltfIn.fields[1], 
            operation=AddDataTranslationOperation{
                arguments={
                    DataFieldAccess{field=myConnMachineOut.fields[0]},
                    IntegerConstantDataOperation{value=100}}
            }
          }
        }
    };

    Service myAnonymizerService = JavaService {
        id = "myAnonymizer",
        name = "myAnonymizer example",
        description = "",
        ver = "0.1.0",
        deployable = true,
        class = "MyAnonymizerImpl",
        artifact = "",
        kind = ServiceKind::TRANSFORMATION_SERVICE,
        input = {{type=refBy(StringType)}},
        output = {{type=refBy(rec1)}}
        // TODO parameter
    };

    Service myKiService = JavaService {
        id = "myKi",
        name = "myKi example",
        description = "",
        ver = "0.1.0",
        asynchronous = false,
        deployable = true,
        class = "MyKiImpl",
        artifact = "",
        kind = ServiceKind::TRANSFORMATION_SERVICE,
        input = {{type=refBy(rec1)}},
        output = {{type=refBy(StringType)}}
        // TODO parameter
    };

    // ------------ application and service nets ------------------
    
    Application myApp = {
        id = "myApp",
        name = "myApp example old",
        ver = "0.1.0",
        description = "",
        services = {refBy(myMesh)}        
    };
    
    ServiceMesh myMesh = {
        description = "initial service net",
        sources = {refBy(mySource), refBy(myOpcConn), refBy(myMqttC)}
    };
    
    MeshSource myOpcConn = {
       impl = refBy(myOpcUaConn)
    };

    MeshSource myMqttC = {
       impl = refBy(myMqttConn)
    };
    
    MeshSource mySource = {
        impl = refBy(mySourceService),
        next = {refBy(myConnMySourceMyAnonymizer)}
    };
    
    MeshConnector myConnMySourceMyAnonymizer = {
        name = "MySource->MyAnonymizer",
        next = refBy(myAnonymizer)
    };

    MeshProcessor myAnonymizer = {
        impl = refBy(myAnonymizerService),
        next = {refBy(myConnMyAnonymizerMyKi)}
    };

    MeshConnector myConnMyAnonymizerMyKi = {
        name = "MyAnonymizer->MyKi",
        next = refBy(myKi)
    };

    MeshProcessor myKi = {
        impl = refBy(myKiService)
    };

    // ---------- generation setup ------------
    
    sharedInterfaces = false;
    // default sharedArtifact, not used in testing

    // ------------ freezing ------------------
    
    freeze {
        aas;
        aasServer;
        aasRegistryServer;
        aasImplServer;
        aasPersistency;
        aasProtocol;
        serializer;
        transportProtocol;
        serviceManager;
        serviceProtocol;
        containerManager;
        deviceMgtStorageServer;
        deviceMgtStorage;
        deviceRegistry;
        javaModuleOpts;
        javaOpts;
        pidDir;
        sharedInterfaces;
        sharedArtifact;
        .; // every variable declared in this project
    } but (f|f.bindingTime >= BindingTime.runtimeMon);

}